apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'


final def keystorePropertiesFile = rootProject.file("keystore.properties")
android {
    compileSdkVersion 30
    defaultConfig {
        applicationId "dev.msfjarvis.powertiles"
        minSdkVersion 30
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"
    }

    buildFeatures.buildConfig = true

    if (keystorePropertiesFile.exists()) {
        final def keystoreProperties = new Properties()
        keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

        signingConfigs {
            release {
                keyAlias keystoreProperties["keyAlias"]
                keyPassword keystoreProperties["keyPassword"]
                storeFile rootProject.file(keystoreProperties["storeFile"])
                storePassword keystoreProperties["storePassword"]
            }
        }
        buildTypes.debug.signingConfig = signingConfigs.release
        buildTypes.release.signingConfig = signingConfigs.release
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles rootProject.file('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        targetCompatibility JavaVersion.VERSION_1_8
        sourceCompatibility JavaVersion.VERSION_1_8
    }
    dependenciesInfo {
        includeInApk false
        includeInBundle false
    }
}

dependencies {
    implementation "androidx.appcompat:appcompat:1.2.0"
    implementation "io.reactivex.rxjava3:rxjava:3.0.6"
}
